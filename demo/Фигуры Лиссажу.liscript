; Фигуры Лиссажу ;

(defmacro sin (x) (java (class "java.lang.Math") "sin" x))
(defmacro cos (x) (java (class "java.lang.Math") "cos" x))
(defmacro round (x) (java x "intValue"))
(defmacro color (r g b) (java (class "java.awt.Color") "new" r g b))

(defmacro add-image (window image)
    (def icon (java (class "javax.swing.ImageIcon") "new"))
    (java icon "setImage" image)
    (def label (java (class "javax.swing.JLabel") "new"))
    (java label "setIcon" icon)
    (java window "add" label))

(def w-s 320 w-s-half (/ w-s 2))
(defmacro norm (x) (+ w-s-half (round (* x w-s-half)) ))

(defn demo (delta a b phi color)
    (def image (java (class "java.awt.image.BufferedImage") "new" w-s w-s 1))
    (def imageGraphics (java image "createGraphics"))
    (java imageGraphics "setBackground" (java (class "java.awt.Color") "new" 255 255 255))
    (java imageGraphics "clearRect" 0 0 w-s w-s)
    (java imageGraphics "setColor" color)

    (defmacro p-x (alpha) (norm (sin (+ (* a alpha) phi))))
    (defmacro p-y (alpha) (norm (cos (* b alpha))))

    (defn next-line (args)
        (match args '(alpha x0 y0))
        (def x (p-x alpha) y (p-y alpha))
        (java imageGraphics "drawLine" x0 (- w-s y0) x (- w-s y))
        (cons (+ alpha delta) x y))

    (ntimes 500 next-line (cons 0 (p-x 0) (p-y 0)))
    (add-image window image))

(def window (java (class "javax.swing.JFrame") "new"))
(java window "setLayout" (java (class "java.awt.FlowLayout") "new"))
(demo 2.5 1 1 0   (color 0 0 0))
(demo 2   2 1 0   (color 255 0 0))
(demo 2   1 2 0.3 (color 0 255 0))
(demo 1   1 3 0.5 (color 255 0 255))
(demo 1   3 1 0.5 (color 0 0 255))
(demo 0.6 3 2 0.5 (color 0 255 255))
(java window "setVisible" true)
;(java window "pack");
(java window "setBounds" 10 10 (+ 50 (* 3 w-s)) (+ 60 (* 2 w-s)))
"Фигуры Лиссажу"
