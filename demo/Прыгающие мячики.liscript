; прыгающие шарики, анимация. Задаем ширину/высоту окна, количество и радиус шариков ;
(defn bouncing-balls (w-width w-height n-balls r-ball)
    (def d-ball (* 2 r-ball)) ; диаметр шарика ;

    ; параметры окна и графики, вызываются макросы стандартной библиотеки ;
    (def image (new-image w-width w-height))
    (def img-gr (new-imageGraphics image))
    (java img-gr "setBackground" (java (class "java.awt.Color") "new" 255 255 200))
    (def window (new-window "FlowLayout"))
    (add-image window image)
    (java window "setLocationByPlatform" true)
    (java window "setVisible" true)
    (java window "setAlwaysOnTop" true)
    (java window "setTitle" (java "Bouncing balls" "toString"))
    (java window "pack")

    ; макросы, которым место в стандартной библиотеке ;
    (defmacro rnd () (java (class "java.lang.Math") "random"))
    (defmacro round (x) (java x "intValue"))

    ; смещение координат шарика по одной оси, возвращает новые координату/скорость ;
    (defn move-dir (x dx x-max)
        (def x-new (+ x dx))
        (cond (< x-new r-ball)           (cons r-ball (- 0 dx))
              (> x-new (- x-max r-ball)) (cons (- x-max r-ball) (- 0 dx))
                                         (cons x-new dx)))

    ; смещает шарик, отрисовывает его и возвращает структуру смещенного шарика ;
    (defn move-draw (ball)
        (match ball '((x dx) (y dy) color))
        (def xdx (move-dir x dx w-width) ydy (move-dir y dy w-height))
        (java img-gr "setColor" color)
        (java img-gr "fillOval" (- (car xdx) r-ball) (- (car ydy) r-ball) d-ball d-ball)
        (cons xdx ydy color))

    ; создает уникальный шарик со своими координатами, скоростью и цветом;
    (defn new-ball ()
        ; структура мяча: ((x dx) (y dy) color) ;
        (defn rnd-dx () (def v (round (+ (* 10 (rnd)) 1))) (cond (< (rnd) 0.5) v (- 0 v)) )
        (defn rnd-rgb () (round (* 200 (rnd)) ))
        (def color (java (class "java.awt.Color") "new" (rnd-rgb) (rnd-rgb) (rnd-rgb)))
        (cons (cons (/ w-width 2) (rnd-dx)) (cons (/ w-height 2) (rnd-dx)) color))

    ; создает список из n шариков ;
    (defn balls-list (n a) (cond (<= n 0) a (balls-list (- n 1) (cons (new-ball) a)) ))

    ; анимация - очистка экрана, смещение и отрисовка шариков, обновление окна, пауза ;
    (defn go (balls)
        (java img-gr "clearRect" 0 0 w-width w-height)
        (def balls-new (map move-draw balls))
        (java window "repaint")
        (java (class "java.lang.Thread") "sleep" 30)
        (go balls-new))

    (go (balls-list n-balls nil)) )

(bouncing-balls 500 400 15 15)